buildscript {
  apply from: "${rootDir}/gradle/versions.gradle"
}

android {
  compileSdkVersion Versions.COMPILE_SDK_VERSION
  
  defaultConfig {
    minSdkVersion Versions.MIN_SDK_VERSION
    targetSdkVersion Versions.TARGET_SDK_VERSION
    versionCode 1
    versionName "1.0"
  
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  
    // The following argument makes the Android Test Orchestrator run its
    // "pm clear" command after each test invocation. This command ensures
    // that the app's state is completely cleared between tests.
    testInstrumentationRunnerArguments clearPackageData: 'true'
    
  }
  
  testOptions {
    execution 'ANDROIDX_TEST_ORCHESTRATOR'
  }
  
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  
}



dependencies {
  implementation project(":kdux-core")
  
  implementation "androidx.lifecycle:lifecycle-extensions:${Versions.androidLifecycle}"
  implementation "androidx.lifecycle:lifecycle-common-java8:${Versions.androidLifecycle}"
  implementation "androidx.appcompat:appcompat:${Versions.androidAppCompat}"
  
  androidTestImplementation "androidx.test:runner:${Versions.androidTest}"
  androidTestUtil "androidx.test:orchestrator:${Versions.androidTest}"
}

task sourceJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  classifier "source"
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId 'org.densebrain'
      artifactId project.name
      version project.version
  
      artifact bundleReleaseAar
      artifact sourceJar {
        classifier "sources"
      }
    }
  }
}
