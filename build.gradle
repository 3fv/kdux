// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  ext {
    VERSION = file("${rootDir}/version.txt").text
    binTrayKey = System.getenv("BINTRAY_API_KEY") ?: ""
  
    /**
     * Publish all local
     */
  
    publishProjects = [
        "kdux-android",
        "kdux-core"
    ]
  }

  apply from: "${rootDir}/gradle/versions.gradle"
  
  repositories {
    google()
    jcenter()
    mavenCentral()
  }
  
  dependencies {
    classpath "com.android.tools.build:gradle:${Versions.androidGradle}"
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${Versions.kotlin}"
    classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:${Versions.bintray}"
  
  }
}

/**
 * Publish install all
 */
task publishAll() {
  dependsOn(build)

  doLast {
    println("All published")
  }
}

/**
 * Publish install all
 */
task publishAllLocal() {
  doLast {
    println("All published locally")
  }
}

task installAllLocal() {
  doLast {
    println("All installed locally")
  }
}

task installLocal() {
  dependsOn publishAllLocal, installAllLocal
}



/**
 * Configure all projects
 */
allprojects {currentProject ->
  
  // SET VERSION
  currentProject.version = VERSION
  
  buildscript {
    apply from: "${rootDir}/gradle/versions.gradle"

  }
 
  // PLUGINS
  apply plugin: 'idea'
  
  
  // IF NOT APP THEN APPLY
  if (!["example-app","kdux-android"].contains(currentProject.name)) {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    
    sourceCompatibility = Versions.SOURCE_VERSION
    targetCompatibility = Versions.SOURCE_VERSION
    
  } else if (currentProject.name == "kdux-android") {
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    //apply plugin: 'kotlin-android-extensions'
  } else {
    apply plugin: 'com.android.application'
    apply plugin: 'kotlin-android'
    //apply plugin: 'kotlin-android-extensions'
    apply plugin: 'kotlin-kapt'
  }
  
  // KOTLIN
  kotlin {
    experimental { coroutines 'enable' }
  }
  
  
  // PUBLISHING
  if (publishProjects.contains(currentProject.name)) {
    apply plugin: 'com.jfrog.bintray'
    
    bintray {
      user = 'jonglanz'
      key = binTrayKey
      publications = ['mavenJava']
      pkg {
        repo = 'oss'
        name = currentProject.name
        userOrg = 'densebrain'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/jglanz/kdux.git'
        version {
          name = VERSION
          released = new java.util.Date()
        }
      }
    }
  
    publishAll.dependsOn(bintrayUpload)
    publishAllLocal.dependsOn(publishToMavenLocal)
    installAllLocal.dependsOn(install)
  }
  
  // REPOS
  repositories {
    google()
    jcenter()
    mavenCentral()
  }

  dependencies {
    implementation(
        "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${Versions.kotlin}",
        "org.jetbrains.kotlin:kotlin-reflect:${Versions.kotlin}",
        "io.arrow-kt:arrow-core:${Versions.arrow}",
        "io.arrow-kt:arrow-typeclasses:${Versions.arrow}",
        "io.arrow-kt:arrow-data:${Versions.arrow}",
        "io.arrow-kt:arrow-syntax:${Versions.arrow}",
        "io.arrow-kt:arrow-effects-kotlinx-coroutines:${Versions.arrow}",
        "org.slf4j:slf4j-api:${Versions.slf4j}"
    )
  
    //testImplementation("junit:junit:${junitVersion}")
  }
}


